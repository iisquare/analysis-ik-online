// 插件
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'

// 项目配置
version = '0.0.1'
archivesBaseName = 'analysis-ik-online'
sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// 插件配置
def pluginClassName = 'com.iisquare.elasticsearch.plugin.IKAnalysisPlugin'
def description = 'Chinese analysis with dictionary online manage.'

// 仓库
repositories {
    maven { url 'https://maven.aliyun.com/repository/central' }
    mavenCentral()
    jcenter()
}

// 依赖
def elasticsearchVersion = '7.9.3'
dependencies {
    compile group: 'org.elasticsearch.client', name: 'transport', version: elasticsearchVersion
    compile group: 'org.codelibs.elasticsearch.lib', name: 'plugin-classloader', version: elasticsearchVersion
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.10'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
}

configurations {
    runtime.exclude group: 'org.elasticsearch.client', module: 'transport'
    runtime.exclude group: 'org.codelibs.elasticsearch.lib', module: 'plugin-classloader'
    runtime.exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
}

// 拷贝配置文件
task generateConfigFiles(type: Copy, dependsOn: [jar]) {
    from('config') {
        include '*.xml', '*.properties', 'plugin-security.policy'
    }
    into 'build/libs'
    doLast {
        def data = [
                'targetCompatibility' : "${targetCompatibility}",
                'elasticsearchVersion': "${elasticsearchVersion}",
                'archivesBaseName'    : "${archivesBaseName}",
                'version'             : "${version}",
                'pluginClassName'     : "${pluginClassName}",
                'description'         : "${description}"
        ]
        def inputFile = new File('config/plugin-descriptor.properties')
        def outputFile = new File('build/libs/plugin-descriptor.properties')
        def printWriter = outputFile.newPrintWriter()
        inputFile.eachLine { line ->
            line = line.replaceAll(/\$\{(\w+)\}/, { data[it[1]] })
            println line
            printWriter.println(line)
        }
        printWriter.flush()
        printWriter.close()
    }
}
//generateConfigFiles.mustRunAfter [clean]

// 部署测试文件
task deployTest(dependsOn: [generateConfigFiles]) {
    for (node in ['single', 'node1', 'node2', 'node3']) {
        delete "server/${node}/plugins/"
        copy {
            from 'build/libs'
            into "server/${node}/plugins/${archivesBaseName}"
            include '*.properties'
        }
    }
}

// 生成插件压缩文件
task release(type: Zip, dependsOn: [clean, generateConfigFiles]) {
    //    into('elasticsearch') {
    from configurations.runtime
    from('build/libs') {
        include '*.jar', '*.xml', '*.properties', '*.policy'
    }
//    }
}
